configurations {
	jaxb
}

buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath 'org.gradle.api.plugins:gradle-tomcat-plugin:0.9.9'
	}
}

ext.tomcatVersion =       '7.0.42'
ext.postgresqlVersion = '9.0-801.jdbc4'
ext.hibernateVersion  = '4.3.11.Final'


apply plugin: 'war'
apply plugin: 'tomcat'

task genJaxb {
	ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
	ext.classesDir = "${buildDir}/classes/jaxb"
	ext.schema = "${projectDir}/src/main/resources/schema.xsd"

	inputs.files schema
	outputs.dir classesDir

	doLast() {
		project.ant {
			taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
					classpath: configurations.jaxb.asPath
			mkdir(dir: sourcesDir)
			mkdir(dir: classesDir)

			xjc(destdir: sourcesDir, schema: schema,
					package: "org.springframework.ws.samples.mtom.schema") {
				produces(dir: sourcesDir, includes: "**/*.java")
			}

			javac(destdir: classesDir, source: 1.6, target: 1.6, debug: true,
					debugLevel: "lines,vars,source",
					classpath: configurations.jaxb.asPath) {
				src(path: sourcesDir)
				include(name: "**/*.java")
				include(name: "*.java")
			}

			copy(todir: classesDir) {
				fileset(dir: sourcesDir, erroronmissingdir: false) {
					exclude(name: "**/*.java")
				}
			}
		}
	}
}


tomcatRun {
	contextPath = 'mtom-server'
}

dependencies {
	compile("org.springframework.ws:spring-ws-core:$springWsVersion")
    compile("org.springframework:spring-context:$springVersion")
	
	// xlitand: using spring aop
	compile("org.springframework:spring-aop:$springVersion")
	
	// xlitand: using aspectj
	compile("org.aspectj:aspectjrt:1.8.10")
	compile("org.aspectj:aspectjweaver:1.8.10")
	compile("org.springframework:spring-aspects:$springVersion")
	
	// xlitand: PostgreSQL Drivers
	compile("postgresql:postgresql:$postgresqlVersion")
	
	// xlitand: JDBC interaction with database
	compile("org.springframework:spring-jdbc:$springVersion")
	
	// xlitand: EmbeddedPostgreSQL by Yandex
	compile("ru.yandex.qatools.embed:embedded-services:1.21")

	// xlitand: To avoid ClassNotFoundException: Kernel32
	//compile("io.fabric8:tooling-jclouds-all:1.2.0.Beta4")

	// https://mvnrepository.com/artifact/net.java.dev.jna/jna
	//compile group: 'net.java.dev.jna', name: 'jna', version: '4.3.0'
	
	// https://mvnrepository.com/artifact/net.java.dev.jna/jna-platform
	//compile group: 'net.java.dev.jna', name: 'jna-platform', version: '4.1.0'
	
	
	// xlitand: for using ServletContextListener, compileOnly in some reason...
	compileOnly group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'

	// xlitand: Hibernate: to start need only this
	compile("org.hibernate:hibernate-core:$hibernateVersion")

	// xlitand: needed for some reason
	compile("org.hibernate:hibernate-entitymanager:$hibernateVersion")
		
	// xlitand: JPA issues in Spring
	compile("org.springframework:spring-orm:$springVersion")
			
	compile(files(genJaxb.classesDir).builtBy(genJaxb))
	
    // xlitand: 'org.apache.log4j.Logger' needs 'compile' in some reason... 
	compile("log4j:log4j:1.2.16")
    runtime("wsdl4j:wsdl4j:1.6.1")

	jaxb "com.sun.xml.bind:jaxb-xjc:2.1.7"	

	tomcat("org.apache.tomcat.embed:tomcat-embed-core:$tomcatVersion",
		   "org.apache.tomcat.embed:tomcat-embed-logging-juli:$tomcatVersion")
	tomcat("org.apache.tomcat.embed:tomcat-embed-jasper:$tomcatVersion") {
		exclude group: 'org.eclipse.jdt.core.compiler', module: 'ecj'
	}
}
